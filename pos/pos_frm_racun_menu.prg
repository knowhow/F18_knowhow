/*
 * This file is part of the bring.out knowhow ERP, a free and open source
 * Enterprise Resource Planning software suite,
 * Copyright (c) 1994-2011 by bring.out doo Sarajevo.
 * It is licensed to you under the Common Public Attribution License
 * version 1.0, the full text of which (including FMK specific Exhibits)
 * is available in the file LICENSE_CPAL_bring.out_knowhow.md located at the
 * root directory of this source code archive.
 * By using this software, you agree to be bound by its terms.
 */

#include "f18.ch"


FUNCTION pos_unos_ispravka_racuna()

   LOCAL lRet
   LOCAL lNoviRacun := .T.

   SetKXLat( "'", "-" )

   o_pos_tables()

   SELECT _pos_pripr

   IF reccount2() <> 0 .AND. AllTrim( field->brdok ) == "PRIPRE"
      lNoviRacun := .F.
   ENDIF

   unos_stavki_racuna( lNoviRacun )

   SET KEY "'" to

   my_close_all_dbf()

   RETURN lRet



STATIC FUNCTION unos_stavki_racuna( lNovi )

   LOCAL cSto := Space( 3 )

   SELECT _pos_pripr
   GO TOP

   IF lNovi
      cBrojRacuna := "PRIPRE"
   ELSE
      cBrojRacuna := _pos_pripr->brdok
      cSto := _pos_pripr->sto
   ENDIF



   pos_unos_racuna( cBrojRacuna, cSto )

   RETURN .T.





FUNCTION zakljuci_pos_racun()

   LOCAL lRet := .F.
   LOCAL lOtvoriUnos := fetch_metric( "pos_konstantni_unos_racuna", my_user(), "N" ) == "D"
   LOCAL _param := hb_Hash()

   O__POS_PRIPR
   my_dbf_pack()

   IF _pos_pripr->( RecCount2() ) == 0
      my_close_all_dbf()
      RETURN lRet
   ENDIF

   GO TOP

   _param[ "idpos" ] := _pos_pripr->idpos
   _param[ "idvd" ] := _pos_pripr->idvd
   _param[ "datum" ] := _pos_pripr->datum
   _param[ "brdok" ] := _pos_pripr->brdok
   _param[ "idpartner" ] := Space( 6 )
   _param[ "idvrstap" ] := "01"
   _param[ "zakljuci" ] := "D"
   _param[ "uplaceno" ] := 0

   form_zakljuci_racun( @_param )

   IF _param[ "zakljuci" ] == "D"
      lRet := azuriraj_stavke_racuna_i_napravi_fiskalni_racun( _param )
   ENDIF

   my_close_all_dbf()

   IF lOtvoriUnos .AND. lRet
      pos_unos_ispravka_racuna()
      lRet := zakljuci_pos_racun()
   ENDIF

   RETURN lRet



STATIC FUNCTION azuriraj_stavke_racuna_i_napravi_fiskalni_racun( hParams )

   LOCAL _br_dok := hParams[ "brdok" ]
   LOCAL _id_pos := hParams[ "idpos" ]
   LOCAL _id_vrsta_p := hParams[ "idvrstap" ]
   LOCAL _id_partner := hParams[ "idpartner" ]
   LOCAL _uplaceno := hParams[ "uplaceno" ]
   LOCAL lOk := .T.
   LOCAL lRet := .F.
   LOCAL cVrijemeRacuna
   LOCAL cBrojRacuna

   o_pos_tables()

   IF ( Len( aRabat ) > 0 )
      ReCalcRabat( _id_vrsta_p )
   ENDIF

   SELECT pos_doks

   cBrojRacuna := pos_novi_broj_dokumenta( _id_pos, POS_VD_RACUN )

   cVrijemeRacuna := PadR( Time(), 5 )
   gDatum := Date()

   lOk := pos_azuriraj_racun( _id_pos, cBrojRacuna, cVrijemeRacuna, _id_vrsta_p, _id_partner )

   IF !lOk
      MsgBeep( "Greška sa ažuriranjem računa u kumulativ !" )
      RETURN lRet
   ENDIF

   // IF gRnInfo == "D"
   pos_racun_info( cBrojRacuna )
   // ENDIF

   IF fiscal_opt_active()
      pos_stampa_fiskalni_racun( _id_pos, gDatum, cBrojRacuna, _uplaceno )
   ENDIF

   my_close_all_dbf()

   RETURN lOk




STATIC FUNCTION pos_stampa_fiskalni_racun( cIdPos, dDatum, cBrRn, nUplaceno )

   LOCAL nDeviceId
   LOCAL hDeviceParams
   LOCAL lRet := .F.
   LOCAL nError := 0

   nDeviceId := odaberi_fiskalni_uredjaj( NIL, .T., .F. )
   IF nDeviceId > 0
      hDeviceParams := get_fiscal_device_params( nDeviceId, my_user() )
      IF hDeviceParams == NIL
         RETURN lRet
      ENDIF
   ELSE
      RETURN lRet
   ENDIF

   nError := pos_fiskalni_racun( cIdPos, dDatum, cBrRn, hDeviceParams, nUplaceno )

   IF nError = -20
      IF Pitanje(, "Da li je nestalo trake u fiskalnom uređaju (D/N)?", "N" ) == "N"
         nError := 20
      ENDIF
   ENDIF

   IF nError > 0
      MsgBeep( "Greška pri štampi fiskalog računa " + cBrRn + " !?##Račun se iz tog razloga BRIŠE")
      pos_povrat_rn( cBrRn, dDatum )
   ENDIF

   lRet := .T.

   RETURN lRet




// ------------------------------------------------------
// ova funkcija treba da izracuna kusur
// ------------------------------------------------------
STATIC FUNCTION koliko_treba_povrata_kupcu( hParams )

   LOCAL nDbfArea := Select()
   LOCAL nTrec := RecNo()
   LOCAL _id_pos := hParams[ "idpos" ]
   LOCAL _id_vd := hParams[ "idvd" ]
   LOCAL _br_dok := hParams[ "brdok" ]
   LOCAL _dat_dok := hParams[ "datum" ]
   LOCAL _total := 0
   LOCAL _iznos := 0
   LOCAL _popust := 0

   SELECT _pos_pripr
   GO TOP

   DO WHILE !Eof() .AND. AllTrim( field->brdok ) == "PRIPRE"
      _iznos += field->kolicina * field->cijena
      _popust += field->kolicina * field->ncijena
      SKIP
   ENDDO

   _total := ( _iznos - _popust )

   SELECT ( nDbfArea )
   GO ( nTrec )

   RETURN _total


STATIC FUNCTION ispisi_iznos_i_kusur_za_kupca( uplaceno, iznos_rn, pos_x, pos_y )

   LOCAL _vratiti := uplaceno - iznos_rn

   IF uplaceno <> 0
      @ pos_x, pos_y + 28 SAY "Iznos RN: " + AllTrim( Str( iznos_rn, 12, 2 ) ) + ;
         " vratiti: " + AllTrim( Str( _vratiti, 12, 2 ) ) ;
         COLOR "BR+/B"
   ENDIF

   RETURN .T.



STATIC FUNCTION form_zakljuci_racun( hParams )

   LOCAL _def_partner := .F.
   LOCAL _id_vd := hParams[ "idvd" ]
   LOCAL _id_pos := hParams[ "idpos" ]
   LOCAL _br_dok := hParams[ "brdok" ]
   LOCAL _dat_dok := hParams[ "datum" ]
   LOCAL _ok := hParams[ "zakljuci" ]
   LOCAL _id_vrsta_p := hParams[ "idvrstap" ]
   LOCAL _id_partner := hParams[ "idpartner" ]
   LOCAL _uplaceno := hParams[ "uplaceno" ]
   LOCAL GetList := {}

   _id_vrsta_p := gGotPlac


   Box(, 8, 67 )

   SET CURSOR ON

   // 01 - gotovina
   // KT - kartica
   // VR - virman
   // CK - cek

   @ box_x_koord() + 1, box_y_koord() + 2 SAY8 "FORMA ZAKLJUČENJA RAČUNA" COLOR "BG+/B"

   @ box_x_koord() + 3, box_y_koord() + 2 SAY8 "Način plaćanja (01/KT/VR...):" GET _id_vrsta_p PICT "@!" VALID p_vrstep( @_id_vrsta_p )

   READ

   IF _id_vrsta_p <> gGotPlac
      _def_partner := .T.
   ENDIF

   IF _def_partner
      @ box_x_koord() + 4, box_y_koord() + 2 SAY "Kupac:" GET _id_partner PICT "@!" VALID p_partner( @_id_partner )
   ELSE
      _id_partner := Space( 6 )
   ENDIF

   @ _x_pos := box_x_koord() + 5, _y_pos := box_y_koord() + 2 SAY8 "Kupac uplatio:" GET _uplaceno PICT "9999999.99" ;
      VALID {|| IF ( _uplaceno <> 0, ispisi_iznos_i_kusur_za_kupca( _uplaceno, koliko_treba_povrata_kupcu( hParams ), _x_pos, _y_pos ), .T. ), .T. }

   @ box_x_koord() + 8, box_y_koord() + 2 SAY8 "Ažurirati POS račun (D/N) ?" GET _ok PICT "@!" VALID _ok $ "DN"

   READ

   BoxC()

   IF LastKey() == K_ESC
      _ok := "D"
   ENDIF

   hParams[ "zakljuci" ] := _ok
   hParams[ "idpartner" ] := _id_partner
   hParams[ "idvrstap" ] := _id_vrsta_p
   hParams[ "uplaceno" ] := _uplaceno

   RETURN .T.



FUNCTION RacObilj()

   IF AScan ( aVezani, {| x | x[ 1 ] + DToS( x[ 4 ] ) + x[ 2 ] == pos_doks->( IdPos + DToS( datum ) + BrDok ) } ) > 0
      RETURN .T.
   ENDIF

   RETURN .F.


/*
FUNCTION PreglNezakljRN()

   o_pos_tables()

   dDatOd := Date()
   dDatDo := Date()

   Box (, 1, 60 )
   SET CURSOR ON
   @ box_x_koord() + 1, box_y_koord() + 2 SAY "Od datuma:" GET dDatOd
   @ box_x_koord() + 1, box_y_koord() + 22 SAY "Do datuma:" GET dDatDo
   READ
   ESC_BCR
   BoxC()

   IF Pitanje(, "Pregledati nezaključene račune (D/N) ?", "D" ) == "D"
      StampaNezakljRN( gIdRadnik, dDatOd, dDatDo )
   ENDIF

   RETURN .T.
*/


/*
FUNCTION RekapViseRacuna()

   cBrojStola := Space( 3 )

   o_pos_tables()

   dDatOd := Date()
   dDatDo := Date()

   Box (, 2, 60 )
   SET CURSOR ON
   @ box_x_koord() + 1, box_y_koord() + 2 SAY "Od datuma:" GET dDatOd
   @ box_x_koord() + 1, box_y_koord() + 22 SAY "Do datuma:" GET dDatDo
   @ box_x_koord() + 2, box_y_koord() + 2 SAY "Broj stola:" GET cBrojStola VALID !Empty( cBrojStola )
   READ
   ESC_BCR
   BoxC()

   IF Pitanje(, "Štampati zbirni račun (D/N) ?", "D" ) == "D"
  --    StampaRekap( gIdRadnik, cBrojStola, dDatOd, dDatDo, .T. )
   ENDIF

   RETURN .T.

*/


FUNCTION StrValuta( cNaz2, dDat )

   LOCAL nTekSel

   nTekSel := Select()
   SELECT valute
   SET ORDER TO TAG "NAZ2"
   cNaz2 := PadR( cNaz2, 4 )
   SEEK PadR( cNaz2, 4 ) + DToS( dDat ) // valute
   IF valute->naz2 <> cnaz2
      SKIP -1
   ENDIF
   SELECT ( nTekSel )
   IF valute->naz2 <> cnaz2
      RETURN 0
   ELSE
      RETURN valute->kurs1
   ENDIF
